/* constants */
TRUE: 1 ;
FALSE: 0 ;

/* incrementors */

--: 1 - ;
++: 1 + ;
pred: dup -- ;
succ: dup ++ ;

/* shuffle words */

slip: q< eval q> ;
dip: swap slip ;
swapd: q< swap q> ; 
roll: swap swapd ;
rot: q< swap q> swap ;
dupd: q< dup q> ;
dup2: dupd swap dupd swap ;
run: dup slip ;

/* inquisitive */

not: 0 = ;
ne?: = not ;
eqz?: 0 = ;
neqz?: 0 = not ;
divisor?: % eqz? ;
even?: 2 divisor? ;
odd?: even? not ;

/* binary operators */

bxor: dup2 ~ & roll swap ~ & | ;

/* logical */

and: * ;
or: + ;

nand: * eqz? ;
and: nand not ;
nor: + eqz? ;

/* branching */

choose: not &swap ? drop ;
branch: choose eval ;
ifte: rot branch ;

/* stack */

empty?: depth 0 = ;
one?: depth 1 = ;

tail: q< clr q> ;
head: &drop reduce ;

/* counters */

repeat_loop: run q> repeat_next ;
repeat_next: dup -- q< &repeat_loop ? ;
repeat: repeat_next q> drop drop ;  

range: dupd swap - &succ swap repeat ;
count: 0 swap ++ &succ swap repeat drop ;

reduce: depth 2 - repeat ;

sum: &+ reduce ;
product: &* reduce ;

/* math */
sqr: dup * ;
!: dup [ dup -- ! * ] [ drop 1 ] ifte ;

/* string printing */

print: dup [ putc print ] &drop ifte ;
println: print 10 putc ;

/* testing */

assert: eqz? [ 0 10 'Failed println 1 exit ] ? ;